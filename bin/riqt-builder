#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';
use 5.008001;
use Getopt::Long;
use Cwd qw(cwd);
use File::Find qw(find);
use File::Spec::Functions qw(
  catdir splitpath splitdir catpath rel2abs abs2rel
);
use File::Spec::Unix;
use File::Copy qw(copy);
use File::Path qw(mkpath rmtree);
use B qw(perlstring);

my $file = shift @ARGV || die "Usage: $0 <bin/runner> <bin/riqt>\n";
my $out_file = shift @ARGV || die "Usage: $0 <bin/runner> <bin/riqt>\n";

unless ( -r $file ) {
    die "$file is not readable";
}

my $cwd = cwd;
my @dirs = grep -d, map rel2abs($_, $cwd), ('lib','fatlib');

my %files;
foreach my $dir (@dirs) {
    find(sub {
        return unless -f $_;
        $File::Find::name =~ /PIQT/ or $dir =~ /fat/ or return;
        !/\.pm$/ and return;
        print STDERR "INCLUDE ${File::Find::name}\n";
        $files{File::Spec::Unix->abs2rel($File::Find::name,$dir)} = do {
          local (@ARGV, $/) = ($File::Find::name); <>
        };
        close ARGV;
    }, $dir);
}

my $build_date = `date +%Y-%m-%dT%H:%M:%S%z`;
chomp $build_date;

my $header = qq!#
# DO NOT MODIFY THIS FILE DIRECTLY
# This entire file was generated by $0
#
# Modify the files under lib/ and then re-run the builder script. External
# library files from fatlib/ are automatically included as well.
#
# Built-On: $build_date
#

# Build information
BEGIN {
    our \$FATPACKED = 'YES';
    our \$FATPACK_BUILD_DATE = '$build_date';
}
!;

my $start = q!
# Taken from App::FatPacker.
BEGIN {
our %fatpacked;
!;

my $end = q!
s/^  //mg for values %fatpacked;

unshift @INC, sub {
  if (my $fat = $fatpacked{$_[1]}) {
    if ($] < 5.008) {
      return sub {
        return 0 unless length $fat;
        $fat =~ s/^([^\n]*\n?)//;
        $_ = $1;
        return 1;
      };
    }
    open my $fh, '<', \$fat
      or die "FatPacker error loading $_[1] (could be a perl installation issue?)";
    return $fh;
  }
  return
};

} # END OF FATPACK-ISH CODE
!;

my @segments = map {
    (my $stub = $_) =~ s/\.pm$//;
    my $name = uc join '_', split '/', $stub;
    my $data = $files{$_}; $data =~ s/^/  /mg; $data =~ s/(?<!\n)\z/\n/;
    '$fatpacked{'.perlstring($_).qq!} = <<'${name}';\n!
    .qq!${data}${name}\n!;
} sort keys %files;

my $shebang = "";
my $script = "";

open my $fh, "<", $file or die("Can't read $file: $!");
$shebang = <$fh>;
$script = join "", <$fh>;
close $fh;
print STDERR "INCLUDE $file\n";

unless ( index($shebang, '#!') == 0 ) {
    $script = $shebang . $script;
    $shebang = "";
}

open my $out, '>', $out_file or die("Can't open $out_file for writing: $!");
print { $out } join "\n", $shebang, $header, $start, @segments, $end, $script;
print STDERR "WROTE $out_file\n";
close $out;

chmod 0755, $out_file;

print STDERR "SHA1SUM ";
system("sha1sum $out_file");

print STDERR "PERL5-COMPILE ";
system("perl -c $out_file");

